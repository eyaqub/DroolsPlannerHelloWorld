import com.hill.planner.basic.Plane;
import org.joda.time.Interval;
import org.drools.planner.core.score.constraint.ConstraintType;
import org.drools.planner.core.score.constraint.IntConstraintOccurrence;
import org.drools.planner.core.score.buildin.hardandsoft.HardAndSoftScoreHolder;

global HardAndSoftScoreHolder scoreHolder;

rule "singlePlaneTaxiRule"
//one plane can be taxing or taking off at any given time
	when
		$plane : Plane($taxiInterval1 : taxiInterval, $id : id);
		$plane2 : Plane($taxiInterval2 : taxiInterval, id != $id);
	then
		int overlap = 0;
		if($taxiInterval1.overlap($taxiInterval2)!=null)
		{
			overlap = ((int)$taxiInterval1.overlap($taxiInterval2).toDurationMillis()/1000);
		}
		insertLogical(new IntConstraintOccurrence("singlePlaneTaxiRule", ConstraintType.NEGATIVE_HARD,
             			overlap,$plane,$plane2));

end

rule "tsatAfterTTOTRule"
	when
		$plane : Plane($tobt : tobt, $tsat : tsat != null && $tsat.isBefore($tobt));
	then
		int difference = (int) (new Interval($tsat, $tobt).toDurationMillis())/10000;
		
		insertLogical(new IntConstraintOccurrence("tsatAfterTTOTRule", ConstraintType.NEGATIVE_HARD,
             			difference,$plane));
end

rule "minimiseDelayRule"
	when
		$plane : Plane($tobt : tobt, $tsat : tsat != null, $tobt.isBefore($tsat));
	then		
		int delay = (int) (new Interval($tobt, $tsat).toDuration().getStandardSeconds());
		insertLogical(new IntConstraintOccurrence("minimiseDelayRule", ConstraintType.NEGATIVE_SOFT,
              			delay,$plane));	
end



// Accumulate hard constraints
rule "hardConstraintsBroken"
        salience -1 // Do the other rules first (optional, for performance)
    when
        $hardTotal : Number() from accumulate(
            IntConstraintOccurrence(constraintType == ConstraintType.NEGATIVE_HARD, $weight : weight),
            sum($weight)
        )
    then
        scoreHolder.setHardConstraintsBroken($hardTotal.intValue());
               			
        //System.out.println("H Score \t"+$hardTotal.intValue());
end

// Accumulate soft constraints
rule "softConstraintsBroken"
        salience -2 // Do the other rules first (optional, for performance)
    when
        $softTotal : Number() from accumulate(
            IntConstraintOccurrence(constraintType == ConstraintType.NEGATIVE_SOFT, $weight : weight),
            sum($weight)
        )
    then
        scoreHolder.setSoftConstraintsBroken($softTotal.intValue());
               			
        //System.out.println("S Score \t"+$softTotal.intValue());
end